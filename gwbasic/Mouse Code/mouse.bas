10 'GWBASIC MOUSE DEMO for ASM Mouse Routines - By RetroNick
20 'Created April 6 - 2022
30 'Last Updated April 8 - 2022
40 'MIT License
45 'See video link
50 SCREEN 7
60 LINE(0,0)-(319,199),1,BF
70 LINE(50,50)-(70,70),2,BF
80 LINE(150,50)-(190,70),3,BF
100 DIM MCODE%(120),shape%(32)
105 I%=0:JC=0:AX%=0:BX%=0:CX%=0:DX%=0
110 GOSUB 60000     'read mouse shape data
150 MEM%=VARPTR(MCODE%(1))
160 RESTORE 1000
200 FOR I%=1 TO  97
300   READ JC
400   POKE MEM%,JC
450   MEM%=MEM%+1
500 NEXT I%
550 'AX% = 0 = Init mouse -return AX% value is not zero you have a mouse, 
560 'BX return indicates number of buttons on the mouse
600 AX%=0 
700 MOUSE=VARPTR(MCODE%(1))
800 CALL MOUSE(AX%,BX%,CX%,DX%)
805 PRINT "INIT RESULT AX=";AX%;" BX=";BX%;" CX=";CX%;" DX=";DX%
810 AX%=1 'Mouse Show
815 MOUSE=VARPTR(MCODE%(1))
820 CALL MOUSE(AX%,BX%,CX%,DX%)
825 DX%=VARPTR(shape%(0)):AX%=9:BX%=5:CX%=5
826 MOUSE=VARPTR(MCODE%(1))
830 CALL MOUSE(AX%,BX%,CX%,DX%)
900 'PRINT "AX=";AX%;" BX=";BX%;" CX=";CX%;" DX=";DX%
910 WHILE INKEY$=""
915   AX%=3
920   MOUSE=VARPTR(MCODE%(1))
930   CALL MOUSE(AX%,BX%,CX%,DX%)
940   LOCATE 23,1: print "ROW=";DX%;"COL=";CX%/2;"STATUS=";BX%;"        "
960 WEND
970 AX%=2 'Mouse Hide
980 MOUSE=VARPTR(MCODE%(1))
985 CALL MOUSE(AX%,BX%,CX%,DX%)
990 WHILE INKEY$="":WEND: SCREEN 0: WIDTH 80: END
1000 REM   Mouse Driver Code, Data Count = 97
1010 DATA  &H55,&H8B,&HEC,&H8B,&H76,&H0C,&H8B,&H04,&H3D,&H00,&H00,&H74,&H17,&H3D
1020 DATA  &H01,&H00,&H74,&H21,&H3D,&H02,&H00,&H74,&H1C,&H3D,&H03,&H00,&H74,&H1C
1030 DATA  &H3D,&H09,&H00,&H74,&H2B,&HEB,&H3A,&H90,&HCD,&H33,&H8B,&H76,&H0C,&H89
1040 DATA  &H1C,&H8B,&H76,&H0A,&H89,&H1C,&HEB,&H2B,&H90,&HCD,&H33,&HEB,&H26,&H90
1050 DATA  &HCD,&H33,&H8B,&H76,&H0A,&H89,&H1C,&H8B,&H76,&H08,&H89,&H0C,&H8B,&H76
1060 DATA  &H06,&H89,&H14,&HEB,&H12,&H90,&H8B,&H76,&H06,&H8B,&H14,&H8B,&H76,&H08
1070 DATA  &H8B,&H0C,&H8B,&H76,&H0A,&H8B,&H1C,&HCD,&H33,&H5D,&HCA,&H08,&H00
60000 RESTORE 61030
60010 FOR I%=0 to 31 
60020   READ shape%(I%)
60030 NEXT
60040 RETURN
61020 ' gwm1
61030 DATA &HFC3F,&HFC3F,&HFC3F,&HFC3F,&HFC3F,&HFC3F,&H03C0,&H03C0,&H03C0,&HFC3F
61040 DATA &HFC3F,&HFC3F,&HFC3F,&HFC3F,&HFC3F,&HFC3F,&H03C0,&H0240,&H0240,&H0240
61050 DATA &H0240,&H0240,&HFFFF,&H83C1,&HFFFF,&H0240,&H0240,&H0240,&H0240,&H0240
61060 DATA &H0240,&H03C0







